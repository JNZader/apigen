plugins {
    id 'java-platform'
    id 'maven-publish'
}

group = 'com.jnzader'
version = '1.0.0-SNAPSHOT'
description = 'APiGen Bill of Materials - Centralized dependency versions'

javaPlatform {
    allowDependencies()
}

dependencies {
    // Spring Boot BOM
    api platform('org.springframework.boot:spring-boot-dependencies:4.0.0')
    api platform('org.springframework.cloud:spring-cloud-dependencies:2024.0.1')

    // APiGen modules constraints
    constraints {
        api "com.jnzader:apigen-core:${version}"
        api "com.jnzader:apigen-security:${version}"
        api "com.jnzader:apigen-codegen:${version}"

        // Third-party versions
        api 'org.mapstruct:mapstruct:1.6.3'
        api 'org.mapstruct:mapstruct-processor:1.6.3'
        api 'com.github.ben-manes.caffeine:caffeine:3.2.3'
        api 'io.jsonwebtoken:jjwt-api:0.13.0'
        api 'io.jsonwebtoken:jjwt-impl:0.13.0'
        api 'io.jsonwebtoken:jjwt-jackson:0.13.0'
        api 'io.github.resilience4j:resilience4j-spring-boot3:2.3.0'
        api 'io.github.resilience4j:resilience4j-circuitbreaker:2.3.0'
        api 'io.github.resilience4j:resilience4j-ratelimiter:2.3.0'
        api 'io.github.resilience4j:resilience4j-retry:2.3.0'
        api 'io.github.resilience4j:resilience4j-micrometer:2.3.0'
        api 'io.micrometer:micrometer-tracing-bridge-otel:1.6.2'
        api 'io.opentelemetry:opentelemetry-exporter-otlp:1.53.0'
        api 'net.logstash.logback:logstash-logback-encoder:9.0'
        api 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'
        api 'com.github.jsqlparser:jsqlparser:5.0'
        api 'com.tngtech.archunit:archunit-junit5:1.4.1'
        api 'org.awaitility:awaitility:4.3.0'
        api 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
        api 'org.testcontainers:testcontainers-junit-jupiter:2.0.3'
        api 'org.testcontainers:testcontainers-postgresql:2.0.3'

        // Feature Flags
        api 'org.togglz:togglz-spring-boot-starter:4.4.0'
        api 'org.togglz:togglz-console:4.4.0'

        // Redis (distributed caching)
        api 'org.springframework.boot:spring-boot-starter-data-redis'

        // Hypersistence Utils (N+1 detection, query analysis)
        api 'io.hypersistence:hypersistence-utils-hibernate-70:3.14.1'
    }
}

publishing {
    publications {
        bom(MavenPublication) {
            from components.javaPlatform

            pom {
                name = 'APiGen BOM'
                description = 'Bill of Materials for APiGen dependencies'
                url = 'https://github.com/jnzader/apigen'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'jnzader'
                        name = 'JN Zader'
                    }
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}
