buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:4.0.2'
    }
}

plugins {
    id 'java'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.jnzader'
version = '1.0.0-SNAPSHOT'
description = 'APiGen Server - REST API for code generation'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux' // For WebClient (GitHub API)
    implementation 'org.springframework.boot:spring-boot-starter-actuator' // Health checks

    // Apache HttpClient 5 - Alternative to Netty for WebClient (avoids Netty 4.2.x strict URI validation)
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.5'
    implementation 'org.apache.httpcomponents.core5:httpcore5-reactive:5.3.4'

    // APiGen modules (local)
    implementation project(':apigen-codegen')
    // Only use apigen-core for exception classes (ProblemDetail, ValidationException, etc.)
    // Exclude JPA/Hibernate to avoid DataSource auto-configuration
    implementation(project(':apigen-core')) {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-data-jpa'
        exclude group: 'org.hibernate.orm', module: 'hibernate-envers'
        exclude group: 'org.hibernate.orm', module: 'hibernate-core'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-cache'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-hateoas'
        exclude group: 'org.springframework.data', module: 'spring-data-jpa'
    }

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

test {
    useJUnitPlatform()
}

// Configure Spring Boot to create executable JAR
bootJar {
    archiveBaseName = 'apigen-server'
    archiveVersion = version
}
